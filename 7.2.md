# Chapter 7. Lesson 2. Jetton Master contract

###### tags: `Chapter 7`

We will start with the **Jetton** standard. Jetton, also known as fungible token standard, is number 74. In [this repository](https://github.com/ton-blockchain/TEPs/blob/master/text/0074-jettons-standard.md) with all the standards you have the whole description about how Jetton contracts must function. You can see two contracts here, a master (or minter) contract and a wallet contract.

Let’s go to the specification of wallet smart contracts. A **wallet smart contract** must handle certain messages. For example, transfer and explaining everything that happens and what is caused by transfer. It must handle burn messages. And then it has the get method, `get_wallet_data`, where it has to return the data about itself. So this is the main way to implement the wallet contract.

And here is the **master contract**. There we don't put code — we put the functionality that is supposed to be implemented in a certain way, so other contracts can interact with your contracts of Jetton. They will know exactly how to work with your contracts, because you have master contracts. The minter is actually minting all those Jettons. You will also have wallet contracts for each user of your Jettons and for external services, which sometimes interact with your users’ wallet contracts. Sometimes they interact with your master contract, minter contract. In order for you not to figure out how it works every time, they just agreed on the standard of how they must function. For example, the wallets want to show balances, and don't want to deal with each Jetton type.

We assume that if something doesn't function according to Jetton requirements explained here, it will not be able to be considered as a Jetton — whether these are wallets, explorers or other services. You have to comply with these rules to be considered a Jetton.  And as I mentioned before, this is just a set of contracts. 

We've learned very basics of contract programming so far, but there is a much more complex logic here. Reading Jetton, master, wallet contracts is very good for you to understand, to actually see how it works. This way you practice your knowledge about how cells are composed, what kind of complex logic you can actually implement in your custom contracts, even if they’re not Jetton-related. Jetton and NFT contracts are amazing way for you to learn what can be done, how the syntax and some complex can be implemented.

Let’s dig into the code of Jetton contracts. As I said, there are two contracts. We already have best practices in their implementation. You don't have to use this code, you can implement your own, but it has to comply with the standards.

In [this repository](https://github.com/ton-blockchain/token-contract), we proceed to the **ft** folder (fungible token, or Jetton). Here we see multiple contracts. [Jetton minter](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-minter.fc) and [Jetton wallet](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-wallet.fc) contracts are those we are going to look into the most of all . There are other contract here like [Jetton Minter ICO](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-minter-ICO.fc) — it’s just an implementation of initial coin offering functionality. We're not going to dig into that, but I really encourage you to look into this code and find out how complex the logic could be. This will teach you a lot.

We will use a few more files like [Jetton utils](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-utils.fc) because Jetton utilities have a certain logic that helps the main file of Jetton minter. Also, we have [op-codes](https://github.com/ton-blockchain/token-contract/blob/main/ft/op-codes.fc) and we have [params](https://github.com/ton-blockchain/token-contract/blob/main/ft/params.fc) — these are just utility files that are helping, and we just encapsulate some logic here to reuse it.

Let's proceed. I’ll clone the repository of token contract standard implementation:


```
git clone https://github.com/ton-blockchain/token-contract.git
```

Let’s start with Jetton minter, because this is the core contract that is initializing everything else. At the beginning we see the storage scheme.


```
;; storage scheme
;; storage#_ total_supply:Coins admin_address:MsgAddress content:^Cell jetton_wallet_code:^Cell = Storage;

(int, slice, cell, cell) load_data() inline {
  slice ds = get_data().begin_parse();
  return (
      ds~load_coins(), ;; total_supply
      ds~load_msg_addr(), ;; admin_address
      ds~load_ref(), ;; content
      ds~load_ref()  ;; jetton_wallet_code
  );
}

() save_data(int total_supply, slice admin_address, cell content, cell jetton_wallet_code) impure inline {
  set_data(begin_cell()
            .store_coins(total_supply)
            .store_slice(admin_address)
            .store_ref(content)
            .store_ref(jetton_wallet_code)
           .end_cell()
          );
}
```


We already know this logic encapsulates reading and saving data in the storage. We can use this function anywhere in the code to ease the process of reading the data. First, we have `total_supply`. We keep the `admin_address` — it’s the one who's administrating the whole contract, and some messages could be accepted only from the admin. We see the `content` — it is basically the data that describes what is unique in the token. Here we also have a look into the data standard — there is a standard of how this data should look, including title, description, some other stuff, so explorers and other services can use your contracts metadata to show users what Jetton they're working with.

Finally, there is the `jetton_wallet_code`. You probably know from the first lessons that contracts are, let's say, scaled. We have one main contract — this is minter. It is deploying wallet contracts when it's minting something. Then, wallet contracts can also deploy other wallets. So if I'm sending money to someone who doesn't have a wallet yet, I'm sending it with code. So our wallet contracts also keep the wallet code, and we will see that in a minute.

Now let's go to the repository with standards and see the [standard regarding the data](https://github.com/ton-blockchain/TEPs/blob/master/text/0064-token-data-standard.md).  Here we have certain guides, for example, to create NFT Collection metadata, NFT item metadata, Jetton metadata example — exactly what would be stored there, where we’re just seeing it.

There is important note that for off-chain content layout, you should store the first byte as `0x01`, and this will show to anybody that this URI is pointing to the JSON. So you basically say to anybody who is reading this data: “Hey, I have off-chain data. Have a look at this link, download the JSON, and there you will find everything”.

But if you're storing it on-chain you have to put the `0x00`. This way you show that the key value dictionary is stored in the rest of the memory. This all is basically a trick on how to optimize it. You make your decision, but it's important so you know what is stored inside of the content cell we're talking about.

After reading from storage and saving data to storage we are going to `mint_tokens`. This is a function that is implementing mint functionality, we just define it here.


```
​​() mint_tokens(slice to_address, cell jetton_wallet_code, int amount, cell master_msg) impure {
  cell state_init = calculate_jetton_wallet_state_init(to_address, my_address(), jetton_wallet_code);
  slice to_wallet_address = calculate_jetton_wallet_address(state_init);
  var msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(to_wallet_address)
    .store_coins(amount)
    .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
    .store_ref(state_init)
    .store_ref(master_msg);
  send_raw_message(msg.end_cell(), 1); ;; pay transfer fees separately, revert on errors
}
```

The next thing we have is `recv_internal`, it is the core function for any smart contract. So here you actually read the first parameters of the message. We're going to explain it soon.


```
() recv_internal(int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    (int total_supply, slice admin_address, cell content, cell jetton_wallet_code) = load_data();
```

Then, based on the operation code you received, you run a more extended functionality:

```
    if (op == op::mint()) {
        throw_unless(73, equal_slices(sender_address, admin_address));
        slice to_address = in_msg_body~load_msg_addr();
        int amount = in_msg_body~load_coins();
        cell master_msg = in_msg_body~load_ref();
        slice master_msg_cs = master_msg.begin_parse();
        master_msg_cs~skip_bits(32 + 64); ;; op + query_id
        int jetton_amount = master_msg_cs~load_coins();
        mint_tokens(to_address, jetton_wallet_code, amount, master_msg);
        save_data(total_supply + jetton_amount, admin_address, content, jetton_wallet_code);
        return ();
    }

    if (op == op::burn_notification()) {
        int jetton_amount = in_msg_body~load_coins();
        slice from_address = in_msg_body~load_msg_addr();
        throw_unless(74,
            equal_slices(calculate_user_jetton_wallet_address(from_address, my_address(), jetton_wallet_code), sender_address)
        );
        save_data(total_supply - jetton_amount, admin_address, content, jetton_wallet_code);
        slice response_address = in_msg_body~load_msg_addr();
        if (response_address.preload_uint(2) != 0) {
          var msg = begin_cell()
            .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 011000
            .store_slice(response_address)
            .store_coins(0)
            .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
            .store_uint(op::excesses(), 32)
            .store_uint(query_id, 64);
          send_raw_message(msg.end_cell(), 2 + 64);
        }
        return ();
    }

    if (op == 3) { ;; change admin
        throw_unless(73, equal_slices(sender_address, admin_address));
        slice new_admin_address = in_msg_body~load_msg_addr();
        save_data(total_supply, new_admin_address, content, jetton_wallet_code);
        return ();
    }

    if (op == 4) { ;; change content, delete this for immutable tokens
        throw_unless(73, equal_slices(sender_address, admin_address));
        save_data(total_supply, admin_address, in_msg_body~load_ref(), jetton_wallet_code);
        return ();
    }

    throw(0xffff);
}
```

For example, we kept the functionality of minting inside of this function. We prepare some data, read out something important — we're going to talk about this as well. And then we run this `mint_tokens` function. We don't put all this code here. For example, for `burn_notification`, the logic is quite simple, so we don't encapsulate it into another function. Here you see the core structure.

Let’s overview everything. We have `load_data`, `save_data` — it’s very simple, we already know this. We have some basic logic of handling any message that comes. Then we have mint logic where we prepare to run `mint_tokens` function. We also have `burn_notification` logic — it's a message that comes when some tokens were supposed to be burned. And we have some administration logic. For example, above we have `(op == 3)`, it means changing administrator. Here you just read the current administrator, compare it, and then save the new one.

This is very simple, we already can understand it easily. You change content or turn off the option to delete this logic. If you don't use edible content or if you want to make it edible, you can actually keep it here. Here, the administrator would be able to save data with different metadata, as you can see. This is something basic and needs just a little practice from you to understand how complex it can be sometimes, and what you can use here from what you learned when we were writing the contract.

Of course, we also have `get` methods, quite simple here. You just return whatever you read from here, and you make it available for the outside world. So you can get `total_supply`, `admin_address`, `content`, `jetton_wallet_code`, and get wallet address can help figure out what the wallet address is for someone's address. For example, I have a wallet and I want to get some Jettons. But I want to figure out what my wallet address will be once I get the Jettons, because the Jettons won’t come to the contract of my wallet — they will come to a deployed wallet, Jetton wallet contract. And I need to figure out what my address will be then. This function helps do that.

All right, let's spend a few more moments to understand what is inside. This logic is quite simple. We don't want to handle empty body messages, so we return and execute:


```
() recv_internal(int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
```

Then we want to figure out what are the flags, so we can actually ignore all the bounced messages. We will also stop execution here:


```
if (flags & 1) { ;; ignore all bounced messages
    return ();
}
```

Then we figure out who is the sender. At this point, we read everything that we wanted from `in_msg_full` — this is something very important for figuring out how we make it further.


```
slice sender_address = cs~load_msg_addr();
```

Now, we read out from the message body. We read out the operation code, query ID, then we get everything from the storage, and we start implementing our mint logic.


```
int op = in_msg_body~load_uint(32);
int query_id = in_msg_body~load_uint(64);

(int total_supply, slice admin_address, cell content, cell jetton_wallet_code) = load_data();

if (op == op::mint()) {
    throw_unless(73, equal_slices(sender_address, admin_address));
    slice to_address = in_msg_body~load_msg_addr();
    int amount = in_msg_body~load_coins();
    cell master_msg = in_msg_body~load_ref();
    slice master_msg_cs = master_msg.begin_parse();
    master_msg_cs~skip_bits(32 + 64); ;; op + query_id
    int jetton_amount = master_msg_cs~load_coins();
    mint_tokens(to_address, jetton_wallet_code, amount, master_msg);
    save_data(total_supply + jetton_amount, admin_address, content, jetton_wallet_code);
    return ();
}
```

We will throw a message error, if `mint` was called not by the admin. Again, it depends on the functionality that you want here, this is not a standard. The standard is that the mint function has to be implemented and has to function in a certain way. But you can make it open if you want everyone to be able to mint. It’s up to you.

After this we read out the `in_msg_body` that we get here. As you know, we have the standard of what must be in this message. You cannot change this, because people are going to treat your contract like it's supposed to be able to handle this. In [Jetton master contract](https://github.com/ton-blockchain/TEPs/blob/master/text/0074-jettons-standard.md) scheme you have to handle this message with a certain logic. There always must be `to_address`, `amount`, some `master_msg` — the body of message that you're going to pass once you actually run the mint transaction.

Now let's see the `mint_tokens` function, so we can understand what happens to the `master_msg` for this jetton_amount. So here you can see that Jetton amount is something that is already in `master_msg`. This is a number of tokens that the user will actually get because you already keep it in `master_msg`. This master message is what you are going to pass to the wallet contract, right? This way the wallet will also know how much Jettons are coming with it. So here you increase the Jetton amount by this.

Let's go back to minting tokens and see where this amount goes. The variable `amount` is coming here. This is the amount of TON, real TONs you want to pass along with your Jetton mint transaction. So here you can control how many tons to pass further. This logic is all up to you because it basically regulates how you distribute your tokens. For example, you can ask a person to send 10 TON to the minter. You get payment, you can check that there must be 10 TON sent for each TON to be minted, for example, or whatever. It's important that you can control how many TONs are actually coming and then passing through to the next contract. You can see this here.

Once you actually run `mint_tokens`, some logic has to be provided. You get the `to_address` — this is the receiver. You get the Jetton wallet code. You get the amount, as we said, to forward, and `master_msg`, which is going to be the body of message that will arrive to this new contract.

```
cell calculate_jetton_wallet_state_init(slice owner_address, slice jetton_master_address, cell jetton_wallet_code) inline {
  return begin_cell()
          .store_uint(0, 2)
          .store_dict(jetton_wallet_code)
          .store_dict(pack_jetton_wallet_data(0, owner_address, jetton_master_address, jetton_wallet_code))
          .store_uint(0, 1)
         .end_cell();
}
```

You remember how we were deploying our contracts, right? We were actually calculating the state_init, and this is exactly what is done here, because this function is going to deploy a new wallet, a new contract. So we're using this function that is also available in [Jetton utils](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-utils.fc). We have this function defined here, yes, and we are composing a cell with the `state_init`.

As you remember, we were doing a similar job there, but in TypeScript. Now you see the similar thing in FunC. Here is `jetton_wallet_code`, and next you have to pack the data. The data is going to be the initial data once the contract will be there. We're going to learn more about this once we will overview the wallet code in the next lesson. Here, you have this function to pack Jetton wallet data. In the storage scheme, we will see `balance`, `owner_address`, `jetton_master_address`, `jetton_wallet_code`. This is the moment when we actually pack in this cell and we're putting code in cell to form the `state_init`, the initial state data. This is one of the functions we are going to use.

Let's get back to the minter. So we are calculating the `state_init` because we need to attach it to the message. Then we have `to_address`, so we need to know where to send this message. And what is going to be the address of the contract? It’s just like we did in TypeScript, but here it will do the same thing inside of FunC. So it's a great practice for you. This `to_wallet` address is going to be the destination where we are actually sending this `state_init`, `master_msg`, and `amount`.


```
slice calculate_jetton_wallet_address(cell state_init) inline {
  return begin_cell().store_uint(4, 3)
                     .store_int(workchain(), 8)
                     .store_uint(cell_hash(state_init), 256)
                     .end_cell()
                     .begin_parse();
}
```


Now we’ll use `calculate_jetton_wallet_address` function to calculate the wallet address. Here we are doing something similar. So as you understand, that address will change if `state_init` changes. But we can see that `state_init` and the cell hash is not everything, it's not the whole address. There are certain flags, there's a `workchain` number we also use here from the parameters. By default we use the zero workchain.

Here you can also learn how the address is formed. TypeScript is saving us from such a complexity, but you need to understand this because sometimes you need to compose the same amount of cells, compose the cell with the address you would actually send to when deploying the contract. So this is why it's a very good practice for you to see how the address is composed. We do the same thing like in TypeScript, but here we do it in FunC, so you learn much more here.

So we're composing the address. You can read the documentation about every bit and what it means here. But this is `workchain`, this is the cell hash that means the address will change if the `state_init` changes. And the `state_init` will change if the code or data changes. It's like a domino. This is a cell, and if you want to read out it, you just start parsing. That's how you return a slice. And you remember, in FunC an address is always a slice.


```
() mint_tokens(slice to_address, cell jetton_wallet_code, int amount, cell master_msg) impure {
  cell state_init = calculate_jetton_wallet_state_init(to_address, my_address(), jetton_wallet_code);
  slice to_wallet_address = calculate_jetton_wallet_address(state_init);
  var msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(to_wallet_address)
    .store_coins(amount)
    .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
    .store_ref(state_init)
    .store_ref(master_msg);
  send_raw_message(msg.end_cell(), 1); ;; pay transfer fees separately, revert on errors
}
```


All right, so let's get back to minter. Basically we calculated the `state_init`, attached it, and calculated the wallet address. We put the amount of TON we want to forward and send there. And we also put the `master_msg`, that is going to be our internal transfer message. On arriving, this message will be handled by the wallet contract. We'll talk about this a bit later.


```
if (op == op::burn_notification()) {
    int jetton_amount = in_msg_body~load_coins();
    slice from_address = in_msg_body~load_msg_addr();
    throw_unless(74,
        equal_slices(calculate_user_jetton_wallet_address(from_address, my_address(), jetton_wallet_code), sender_address)
    );
    save_data(total_supply - jetton_amount, admin_address, content, jetton_wallet_code);
    slice response_address = in_msg_body~load_msg_addr();
    if (response_address.preload_uint(2) != 0) {
      var msg = begin_cell()
        .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 011000
        .store_slice(response_address)
        .store_coins(0)
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::excesses(), 32)
        .store_uint(query_id, 64);
      send_raw_message(msg.end_cell(), 2 + 64);
    }
    return ();
}
```


The last part that we'll quickly go through is burn_notification. It is basically when we get the Jetton amount that we want to burn. We figure out what is the address that's sent it — for this we use `calculate_user_jetton_wallet_address` function.

Let's see what it does. So someone is sending the message with `burn_notification`. We figure out how many Jettons we should burn. And then we check from whom this burn notification is coming. Let's check out this function in the [utils](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-utils.fc):


```
slice calculate_jetton_wallet_address(cell state_init) inline {
  return begin_cell().store_uint(4, 3)
                     .store_int(workchain(), 8)
                     .store_uint(cell_hash(state_init), 256)
                     .end_cell()
                     .begin_parse();
}

slice calculate_user_jetton_wallet_address(slice owner_address, slice jetton_master_address, cell jetton_wallet_code) inline {
return calculate_jetton_wallet_address(calculate_jetton_wallet_state_init(owner_address, jetton_master_address, jetton_wallet_code));
}
```


Here we are calculating `state_init` of the owner address. Let's get back to the minter: `from_address` is going to be the owner address, `my_address` is the address of this contract, our minter. And then we have `jetton_wallet_code`. So we check that the results of `calculate_user_jetton_wallet_address` function are the same as the `sender_address`. Whoever is sending this message, `from_address`, that is from whom we should burn these Jettons. So we will calculate the wallet address of this user. We want to make sure that it is going to be the same as the `sender_address`. And then we open this function and see what it does.

It's basically using other two functions that we already know. It's calculating the address and getting the `state_init` out of `owner_address` that we pass here, `jetton_master_address` (this contract’s address) and `jetton_wallet_code`. Out of the `state_init`, we calculate the `jetton_master_address`, and this will make sure that the one who sends this message is the owner of the contract that we're going to actually burn the Jettons from.

Once we do that, we actually deduct the amount of Jettons from the total supply and save this data. Now in the storage, we can see the less amount of Jetton in supply. And then we read the response address. According to the scheme, the body must include the response address — to whom we should deliver the rest of the TON that are there, with the `op::excesses`.

We start forming body here. We're composing the message that is not able to be bounced, so the shortcut is changed to `0x10`. Basically, we use the 2 + 64 raw code. So we send out all the money that came with this message to the response address, along with `op::excesses`.

Basically, that's it. We've sent two contract messages like handlers, and we figured out that these are just contracts. No magic in Jettons, it's just a contract with a certain logic, and simply reading how the contract works gives us many benefits. In the next lesson, we'll learn about Jetton wallet, so you're going to see it from the other side, see how the wallet interacts with it. Also, we’ll return to the [minter](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-minter.fc) sometimes to see some logic, especially at deploy. I wish you good luck with learning this logic with the repositories. See you in the next lesson with the Jetton wallet contract code review!

[Proceed to Lesson 3 >](https://github.com/markokhman/func-course/blob/main/Chapter%207/Lesson%203.md)

[< Go to previous lesson ](https://github.com/markokhman/func-course/blob/main/Chapter%207/Lesson%201.md)
