# Chapter 7. Lesson 4. NFT collection smart contract

###### tags: `Chapter 7`

In the [same repository](https://github.com/ton-blockchain/token-contract/tree/main/ft) that has the fungible token code, we also see [non-fungible token code](https://github.com/ton-blockchain/token-contract/blob/main/nft/nft-collection.fc). This is just a standard implementation of the NFT standard described like Jettons. [Here](https://github.com/ton-blockchain/TEPs/blob/master/text/0062-nft-standard.md) you can find all the metadata examples, in accordance with the metadata standards we reviewed in the previous lessons.

We have two types of contracts here as well: the collection contract and the NFT item smart contract. A collection contract is deploying the NFT item smart contract. A good example here is that if you want to release a collection that contains 10,000 items, you will deploy 10,001 smart contracts. You will deploy it by yourself.

Inside of the item smart contract the owners will be changing. Also, the owner of a collection can be changed. You need to understand this logic. First contract we are going to review is going to be the NFT collection, and then we are going to review the NFT item smart contract. Again, this is not just one contract, this is a system of smart contracts.

**NFT collection** smart contract, like any other contract, starts with the encapsulated logic of reading the storage:


```
(slice, int, cell, cell, cell) load_data() inline {
  var ds = get_data().begin_parse();
  return 
    (ds~load_msg_addr(), ;; owner_address
     ds~load_uint(64), ;; next_item_index
     ds~load_ref(), ;; content
     ds~load_ref(), ;; nft_item_code
     ds~load_ref()  ;; royalty_params
     );
}
```


Here in the storage we have `owner_address`, `next_item_index`, we have `content`. Again, this `content` is a cell made in accordance with the metadata standard that is also available in the same standard [repository](https://github.com/ton-blockchain/TEPs/blob/master/text/0062-nft-standard.md) that we reviewed. Then we have `nft_item_code` and a cell with `royalty_params`. We're going to learn more about those soon.


```
() save_data(slice owner_address, int next_item_index, cell content, cell nft_item_code, cell royalty_params) impure inline {
  set_data(begin_cell()
    .store_slice(owner_address)
    .store_uint(next_item_index, 64)
    .store_ref(content)
    .store_ref(nft_item_code)
    .store_ref(royalty_params)
    .end_cell());
}

cell calculate_nft_item_state_init(int item_index, cell nft_item_code) {
  cell data = begin_cell().store_uint(item_index, 64).store_slice(my_address()).end_cell();
  return begin_cell().store_uint(0, 2).store_dict(nft_item_code).store_dict(data).store_uint(0, 1).end_cell();
}

slice calculate_nft_item_address(int wc, cell state_init) {
  return begin_cell().store_uint(4, 3)
                     .store_int(wc, 8)
                     .store_uint(cell_hash(state_init), 256)
                     .end_cell()
                     .begin_parse();
}
```


Just like with `load_data`, we also have `save_data` to rewrite the cell of this local storage. In the previous example with Jettons, we actually saw these `calculate_nft_item_state_init` and `calculate_nft_item_address` functions were encapsulated in a different file. But here we have them in this standard implementation, in the same file. This is because we don't really use these functions in the NFT item contract, so this is why we don't have to keep it in a separate file. In Jettons, as you remember, we were using these functions both from the minter contract and from the wallet contract. But here we have them all in one file.

You probably don't need any more explanations on `calculate_nft_item_state_init` function because you went through a very complex one in the Jettons lessons. The same is with `calculate_nft_item_address` because you already went through this. Let's also pass `load_data` and `save_data` we're familiar with.

Next we see `deploy_nft_item` and `send_royalty_params` functions:


```
() deploy_nft_item(int item_index, cell nft_item_code, int amount, cell nft_content) impure {
  cell state_init = calculate_nft_item_state_init(item_index, nft_item_code);
  slice nft_address = calculate_nft_item_address(workchain(), state_init);
  var msg = begin_cell()
            .store_uint(0x18, 6)
            .store_slice(nft_address)
            .store_coins(amount)
            .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
            .store_ref(state_init)
            .store_ref(nft_content);
  send_raw_message(msg.end_cell(), 1); ;; pay transfer fees separately, revert on errors
}

() send_royalty_params(slice to_address, int query_id, slice data) impure inline {
  var msg = begin_cell()
    .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 011000
    .store_slice(to_address)
    .store_coins(0)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .store_uint(op::report_royalty_params(), 32)
    .store_uint(query_id, 64)
    .store_slice(data);
  send_raw_message(msg.end_cell(), 64); ;; carry all the remaining value of the inbound message
}
```


This is all done in accordance with the requirements of the standard. The interface mustn’t vary depending on your project because you have to comply with the standard, so your contracts would be considered as NFT contracts.


```
() recv_internal(cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);


    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();

    int op = in_msg_body~load_uint(32);
    int query_id = in_msg_body~load_uint(64);

    var (owner_address, next_item_index, content, nft_item_code, royalty_params) = load_data();
```


We receive an internal message and we see that we don't want to work with empty body messages, so we check if it's empty. Then we check the flags, ignore the bounce messages, we get the `sender_address`, read `in_msg_body`, operation code, `query_id`, and then everything from the local storage.


```
if (op == op::get_royalty_params()) {
    send_royalty_params(sender_address, query_id, royalty_params.begin_parse());
    return ();
}

throw_unless(401, equal_slices(sender_address, owner_address));


```


If operation code is `get_royalty_params`, then we send royalty parameters. As you see, we read royalty parameters from the local storage and we pass royalty parameters to `begin_parse`. `royalty_params` is a cell and we are passing a slice so it can be immediately read or written into the message we're going to send.


```
() send_royalty_params(slice to_address, int query_id, slice data) impure inline {
  var msg = begin_cell()
    .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 011000
    .store_slice(to_address)
    .store_coins(0)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .store_uint(op::report_royalty_params(), 32)
    .store_uint(query_id, 64)
    .store_slice(data);
  send_raw_message(msg.end_cell(), 64); ;; carry all the remaining value of the inbound message
}
```

Let's see how `send_royalty_params` is implemented. We get to_address, we get `query_id`, and `slice_data`, this slice being the `royalty_params`. We don't want this message to be able to bounce, so we change the flag shortcut to `0x10`. This is all familiar to you. We send it to the `to_address`, we put the operation code, the `query_id`, and of course we put these `royalty_params`. It's that simple. You already know how this works. For the royalty parameters, we don't even check who's asking for it. What’s next?

```
if (op == 1) { ;; deploy new nft
  int item_index = in_msg_body~load_uint(64);
  throw_unless(402, item_index <= next_item_index);
  var is_last = item_index == next_item_index; 
  deploy_nft_item(item_index, nft_item_code, in_msg_body~load_coins(), in_msg_body~load_ref());
  if (is_last) {
    next_item_index += 1;
    save_data(owner_address, next_item_index, content, nft_item_code, royalty_params);
  }
  return ();
}
```

We see this operation code, which means we want to deploy a new NFT. It can be done only by the owner. The important thing here is index. You need to track what is the current index of your collection. For example, the `item_index` that you want to deploy here has to be lower than the `next_item_index` stored in our database. And if they're equal, we will actually increase the `next_item_index` and put it into the storage again, so our index would increase. You’d either deploy the earlier index NFTs or deploy the current one, and then you should increase the `next_item_index`.


```
​​() deploy_nft_item(int item_index, cell nft_item_code, int amount, cell nft_content) impure {
  cell state_init = calculate_nft_item_state_init(item_index, nft_item_code);
  slice nft_address = calculate_nft_item_address(workchain(), state_init);
  var msg = begin_cell()
            .store_uint(0x18, 6)
            .store_slice(nft_address)
            .store_coins(amount)
            .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
            .store_ref(state_init)
            .store_ref(nft_content);
  send_raw_message(msg.end_cell(), 1); ;; pay transfer fees separately, revert on errors
}
```

Now have a look at how the deploy function is implemented here. `deploy_nft_item` deploys the contract; this is why we are calculating the `state_init` here. We are calculating the address along with `state_init`. Also, we are sending a message with a certain amount that we put here.

Let's see how we get this amount when we deploy. In `msg_body`, according to the standard, you are going to see what the amount of coins is that you want to pass there, because we're setting this as the `amount`. So we compose this message with `state_init`, containing NFT content, then send and deploy this contract. We are going to see what the local storage scheme is once we are going to review the NFT item contract in the next lesson. Deploying NFT items is much simpler than we did it with Jettons but it works in the similar way.


```
if (op == 2) { ;; batch deploy of new nfts
      int counter = 0;
      cell deploy_list = in_msg_body~load_ref();
      do {
        var (item_index, item, f?) = deploy_list~udict::delete_get_min(64);
        if (f?) {
          counter += 1;
          if (counter >= 250) { ;; Limit due to limits of action list size
            throw(399);
          }

          throw_unless(403 + counter, item_index <= next_item_index);
          deploy_nft_item(item_index, nft_item_code, item~load_coins(), item~load_ref());
          if (item_index == next_item_index) {
            next_item_index += 1;
          }
        }
      } until ( ~ f?);
      save_data(owner_address, next_item_index, content, nft_item_code, royalty_params);
      return ();
    }

if (op == 3) { ;; change owner
  slice new_owner = in_msg_body~load_msg_addr();
  save_data(new_owner, next_item_index, content, nft_item_code, royalty_params);
  return ();
```


There are two more operation codes we should figure out. `Change owner` is just a standard one, very simple for you to understand. So I'm not going to dig deeper into this one.

More important is `batch deploy of new nfts`, it's a bit more complex. For the batch deploy, you actually bring a cell — like you put a cell inside of `msg_body`. And this cell will have a dictionary, so you will run through this whole dictionary and deploy your NFT item step by step. Make sure that your counter never goes higher than **250** because this is the maximum amount of contracts you can deploy as an NFT item from here. Also, you’d better make sure that you keep track of your next item index and how it’s increasing again and again in the local storage. And of course, run the `deploy_nft_item` function on every deploy.

This contract is much simpler than we've done with the Jettons. Again, it is important for you to see that everything is a contract in TON. A very simple logic can create a complex system of contracts. For example, in Jettons we have minter, wallets, and wallets are deploying other wallets. In NFTs we have collections, which deploy items that are also the contracts.

In the next lesson we are going to review the NFT item contract. We're going to see how it actually gets initialized with the initial state we give. And it's going to be more than just a single contract, because items can have different types of contracts. We'll talk about them in the next lesson. So see you very soon.

[Proceed to Lesson 5 >](https://github.com/markokhman/func-course/blob/main/Chapter%207/Lesson%205.md)

[< Go to previous lesson ](https://github.com/markokhman/func-course/blob/main/Chapter%207/Lesson%203.md)