# Chapter 7. Lesson 3. Jetton wallet contract

###### tags: `Chapter 7`

In this lesson, we're going to review the **wallet smart contract**. As you remember, the wallet smart contract is deployed by the minter smart contract once we mint the Jettons. Or the wallet smart contract is deployed by another wallet smart contract when we do transfer. So again, we have certain preparations:


```
int min_tons_for_storage() asm "10000000 PUSHINT"; ;; 0.01 TON
;; Note that 2 * gas_consumptions is expected to be able to cover fees on both wallets (sender and receiver)
;; and also constant fees on inter-wallet interaction, in particular fwd fee on state_init transfer
;; that means that you need to reconsider this fee when:
;; a) jetton logic become more gas-heavy
;; b) jetton-wallet code (sent with inter-wallet message) become larger or smaller
;; c) global fee changes / different workchain
int gas_consumption() asm "15000000 PUSHINT"; ;; 0.015 TON
```


You can read here in the code that we, of course, need to have a certain amount of TONs. We need to deduct them from every transaction that comes here just to make sure that there is a minimum amount available for storage payment. We also do some constant on the `gas_consumption` — this is just an approximation.


```
{-
  Storage
  storage#_ balance:Coins owner_address:MsgAddressInt jetton_master_address:MsgAddressInt jetton_wallet_code:^Cell = Storage;
-}

(int, slice, slice, cell) load_data() inline {
  slice ds = get_data().begin_parse();
  return (ds~load_coins(), ds~load_msg_addr(), ds~load_msg_addr(), ds~load_ref());
}

() save_data (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) impure inline {
  set_data(pack_jetton_wallet_data(balance, owner_address, jetton_master_address, jetton_wallet_code));
}
```


And then of course we do this `load_data`, `save_data` just like we did from the very beginning, when we encapsulate the logic of reading the storage. So let's have a look into the minter where we actually deployed this contract. Here we did the pack Jetton wallet data from [utils](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-utils.fc): put the balance, the owner address, the master_address and jetton_wallet_code. And we can see here coins, address, address, and then cell:


```
return (ds~load_coins(), ds~load_msg_addr(), ds~load_msg_addr(), ds~load_ref());
```


So the code is stored as a cell and we just put it here. Basically, this is a good example of the initial data of the contract once this wallet contract is executed the first time. What kind of functionality do we have here? We have `send_tokens` function, `receive_tokens` function, `burn_tokens` function, `on_bounce`, and this is all defined by us. This is not standard. `recv_internal` is the core one, and here we see a standard thing.

So once the operation code that is coming with the message matches the transfer operation code, or internal transfer, or burn, then we run those functions that we just discussed:


```
int op = in_msg_body~load_uint(32);

if (op == op::transfer()) { ;; outgoing transfer
  send_tokens(in_msg_body, sender_address, msg_value, fwd_fee);
  return ();
}

if (op == op::internal_transfer()) { ;; incoming transfer
  receive_tokens(in_msg_body, sender_address, my_balance, fwd_fee, msg_value);
  return ();
}

if (op == op::burn()) { ;; burn
  burn_tokens(in_msg_body, sender_address, msg_value, fwd_fee);
  return ();
}
```


Here we have five core things that we have to review. One is the preparation here: what happens when we first just see the `in_msg_full` and we start reading in it? So we figure out what to do with it. Then based on the operation code we review the transfer, namely `send_tokens` function that we discussed here. Then we will review the `receive_tokens` function, also available here, and `burn_tokens`. The last thing we need to do is the `get` method, but it’s the simplest one here. So let's start with logic [here](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-wallet.fc):


```
() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
  if (in_msg_body.slice_empty?()) { ;; ignore empty messages
    return ();
  }

  slice cs = in_msg_full.begin_parse();
  int flags = cs~load_uint(4);
  if (flags & 1) {
    on_bounce(in_msg_body);
    return ();
  }
  slice sender_address = cs~load_msg_addr();
  cs~load_msg_addr(); ;; skip dst
  cs~load_coins(); ;; skip value
  cs~skip_bits(1); ;; skip extracurrency collection
  cs~load_coins(); ;; skip ihr_fee
  int fwd_fee = muldiv(cs~load_coins(), 3, 2); ;; we use message fwd_fee for estimation of forward_payload costs
```

Again, we don't want to work with messages that have empty body. So we start reading the `in_msg_full` to see the flags. And again, we detect if the flag says that this is a bounced message. Now we want to handle it and we are going to review it in a moment. Then we figure out what the `sender_address` is. Then we skip some information because we are going to pass the `in_msg_body` into the corresponding functions. So here we don't really care about this.

But next, we do care about the `fwd_fee`. Here we estimate how much was paid for the previous transaction, and now we want to know this sum to actually keep a certain amount on the contract. So we estimate how much it will cost for the next transaction. This is interesting logic that you actually have to learn.

You already know how to compose the message, as we have done that in the previous chapters, with `send_draw_message`. You already know that `send_draw_message` accepts a cell with all these parameters. And this is why we can actually read it out here, because those parameters are present as bits inside of this message cell.


```
  int op = in_msg_body~load_uint(32);

  if (op == op::transfer()) { ;; outgoing transfer
    send_tokens(in_msg_body, sender_address, msg_value, fwd_fee);
    return ();
  }

  if (op == op::internal_transfer()) { ;; incoming transfer
    receive_tokens(in_msg_body, sender_address, my_balance, fwd_fee, msg_value);
    return ();
  }

  if (op == op::burn()) { ;; burn
    burn_tokens(in_msg_body, sender_address, msg_value, fwd_fee);
    return ();
  }
```

All right, so next we go with the **operation codes**. By this moment, we already figured out the bounds, we figured out how much we should actually keep for the next transaction cost. And now we go to the operation codes. So we see these `send_tokens` and we pass `in_msg_body`. We pass everything that comes with the body — the sender address, the message value. As you remember, `msg_value` is how much money came with this message. And the `fwd_fee` is the estimation, it's basically the exact amount that was spent on the forward before this message, and we pass it into the send_tokens.

### Transferring tokens

Let's have a look: 

```
() send_tokens (slice in_msg_body, slice sender_address, int msg_value, int fwd_fee) impure {
  int query_id = in_msg_body~load_uint(64);
  int jetton_amount = in_msg_body~load_coins();
  slice to_owner_address = in_msg_body~load_msg_addr();
  force_chain(to_owner_address);
  (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = load_data();
  balance -= jetton_amount;
```

Send tokens is right here. So we read the `query_id` because we already read out the operation code. We get the `jetton_amount` we want to send, and we read the address. So we do `force_chain`, and then we make sure that we are on the proper chain. Then we read all the data from the storage. We reduce the balance by the digit amount, and now we have to do a few checks.


```
throw_unless(705, equal_slices(owner_address, sender_address));
throw_unless(706, balance >= 0);
```


So we want to make sure that the `sender_address` is the owner. Owner is some real wallet contract, the real wallet contract that is owning this Jetton wallet contract. It is saved here in the storage. We want to make sure that the sender, who actually sends us this message, has the same address as who is owning this wallet. We are going to throw an error if this is not the case. Then we go check that the balance is still bigger or equal to 0 after we reduced the Jetton amount.


```
cell state_init = calculate_jetton_wallet_state_init(to_owner_address, jetton_master_address, jetton_wallet_code);
slice to_wallet_address = calculate_jetton_wallet_address(state_init);
slice response_address = in_msg_body~load_msg_addr();
cell custom_payload = in_msg_body~load_dict();
int forward_ton_amount = in_msg_body~load_coins();
throw_unless(708, slice_bits(in_msg_body) >= 1);
slice either_forward_payload = in_msg_body;
var msg = begin_cell()
  .store_uint(0x18, 6)
  .store_slice(to_wallet_address)
  .store_coins(0)
  .store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1)
  .store_ref(state_init);
var msg_body = begin_cell()
  .store_uint(op::internal_transfer(), 32)
  .store_uint(query_id, 64)
  .store_coins(jetton_amount)
  .store_slice(owner_address)
  .store_slice(response_address)
  .store_coins(forward_ton_amount)
  .store_slice(either_forward_payload)
  .end_cell();
```

Now, we start preparing the message to send for deploying this wallet contract. We use the very same functions from the [Jetton utils](https://github.com/ton-blockchain/token-contract/blob/main/ft/jetton-utils.fc) folder. We're not going into depth here because you already understand how it works. So we calculate the `state_init`, figure out the wallet address, where we're going to send this message. And then we read the `response_address`. So `in_msg_body` will contain who gets the accesses and a notification about who should get the rest of the data once this sequence of transactions is finished. Then we can put custom_payload and we put `forward_ton_amount`, so we know how many TONs we should put inside of this transaction.

So we start to compose the message to send the Jettons. Who we want to send the Jettons to might not have the Jetton wallet contract. That's why we need to attach `state_init` to this message: we are going to deploy the contract if it doesn't have it.

So we have the same shortcuts on the flags in the beginning. Destination goes next. We don't put any coins just now because we will do it when we are going to send it. And next we are composing all these features — something is not working yet, something is going to be rewritten by validators. And next, after the message core you compose the message body.

Here's something interesting you didn't meet before. We can actually continue this code because we didn't end cell in var msg. We can continue putting certain things into this cell.

So we'll put `store_ref` in this message body later, after it's composed. So again, here we say that this is internal transfer. We put the `query_id`, the `jetton_amount`, `owner_address`, `response_address`, how much TONs to forward, and possible forward payload that is going to stay in the `in_msg_body`, possibly.


```
msg = msg.store_ref(msg_body);
  int fwd_count = forward_ton_amount ? 2 : 1;
  throw_unless(709, msg_value >
                     forward_ton_amount +
                     ;; 3 messages: wal1->wal2,  wal2->owner, wal2->response
                     ;; but last one is optional (it is ok if it fails)
                     fwd_count * fwd_fee +
                     (2 * gas_consumption() + min_tons_for_storage()));
                     ;; universal message send fee calculation may be activated here
                     ;; by using this instead of fwd_fee
                     ;; msg_fwd_fee(to_wallet, msg_body, state_init, 15)

  send_raw_message(msg.end_cell(), 64); ;; revert on errors
  save_data(balance, owner_address, jetton_master_address, jetton_wallet_code);
}
```


All right, so let's go to `fwd_count`. We check here that after we execute this transaction,  our contract has still enough money to survive. And only at this point we are ending the cell of the message and send this message. With 64 again, we're passing further the funds that came with this message. You should learn more about this mode and flags in the documentation, we already talked about this. And of course, you save the new data, new balance after we made all these manipulations.

Great, this is the transfer function. This function is going to be called by the main wallet. For example, you have just your normal wallet, like on Tonhub or Tonkeeper. And this wallet contract is going to trigger `send_tokens`. Or there may be some other logic. But you have to be the owner of this Jetton wallet contract to be able to trigger the `send_tokens` operation.

### Receiving tokens
Next one is receiving tokens:


```
() receive_tokens (slice in_msg_body, slice sender_address, int my_ton_balance, int fwd_fee, int msg_value) impure {
  ;; NOTE we can not allow fails in action phase since in that case there will be
  ;; no bounce. Thus check and throw in computation phase.
  (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = load_data();
  int query_id = in_msg_body~load_uint(64);
  int jetton_amount = in_msg_body~load_coins();
  balance += jetton_amount;
  slice from_address = in_msg_body~load_msg_addr();
  slice response_address = in_msg_body~load_msg_addr();
  throw_unless(707,
      equal_slices(jetton_master_address, sender_address)
      |
      equal_slices(calculate_user_jetton_wallet_address(from_address, jetton_master_address, jetton_wallet_code), sender_address)
  );
  int forward_ton_amount = in_msg_body~load_coins();

  int ton_balance_before_msg = my_ton_balance - msg_value;
  int storage_fee = min_tons_for_storage() - min(ton_balance_before_msg, min_tons_for_storage());
  msg_value -= (storage_fee + gas_consumption());
  if(forward_ton_amount) {
    msg_value -= (forward_ton_amount + fwd_fee);
    slice either_forward_payload = in_msg_body;

    var msg_body = begin_cell()
        .store_uint(op::transfer_notification(), 32)
        .store_uint(query_id, 64)
        .store_coins(jetton_amount)
        .store_slice(from_address)
        .store_slice(either_forward_payload)
        .end_cell();

    var msg = begin_cell()
      .store_uint(0x10, 6) ;; we should not bounce here cause receiver can have uninitialized contract
      .store_slice(owner_address)
      .store_coins(forward_ton_amount)
      .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
      .store_ref(msg_body);

    send_raw_message(msg.end_cell(), 1);
  }

  if ((response_address.preload_uint(2) != 0) & (msg_value > 0)) {
    var msg = begin_cell()
      .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 010000
      .store_slice(response_address)
      .store_coins(msg_value)
      .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
      .store_uint(op::excesses(), 32)
      .store_uint(query_id, 64);
    send_raw_message(msg.end_cell(), 2);
  }

  save_data(balance, owner_address, jetton_master_address, jetton_wallet_code);
}
```

Here is how you receive tokens. Again, you read all the data from the storage. We increment the balance. We check that the one who sent us Jettons, sent this information about increasing the balance, is either the master contract or some other contract of a Jetton wallet. And then we read the forward_ton_amount to make sure that we have enough money for staying on the contract. In case there's `forward_ton_amount`, we compose this new message with the `forward_ton_amount` and `forward_payload`, if it's actually available there. Of course, we need to send the accesses in case they're present there. This is why we compose the message about sending all the accesses that are left on this chain of transactions to the response address if it's specified. Then we save the new data here. Whatever will change, it will only change the balance. Great. So this is how we handle receiving tokens.


### Burning tokens
Two more left: how we actually burn them and how to do on_bouce. You see, we don't go deep into every single line and logical cell we compose because it would take us hours to overview that. I want you to understand the logic on a higher level. We could make a chapter for each of these contracts to figure out every single logical case that could be handled here. But I want you to understand high-level logic because you are not going to create these contracts by yourself. You just need to understand how they work so you would actually use these functions because most Jetton contracts are using them. And you will know how to use them for your cases.


```
() burn_tokens (slice in_msg_body, slice sender_address, int msg_value, int fwd_fee) impure {
  ;; NOTE we can not allow fails in action phase since in that case there will be
  ;; no bounce. Thus check and throw in computation phase.
  (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = load_data();
  int query_id = in_msg_body~load_uint(64);
  int jetton_amount = in_msg_body~load_coins();
  slice response_address = in_msg_body~load_msg_addr();
  ;; ignore custom payload
  ;; slice custom_payload = in_msg_body~load_dict();
  balance -= jetton_amount;
  throw_unless(705, equal_slices(owner_address, sender_address));
  throw_unless(706, balance >= 0);
  throw_unless(707, msg_value > fwd_fee + 2 * gas_consumption());

  var msg_body = begin_cell()
      .store_uint(op::burn_notification(), 32)
      .store_uint(query_id, 64)
      .store_coins(jetton_amount)
      .store_slice(owner_address)
      .store_slice(response_address)
      .end_cell();

  var msg = begin_cell()
    .store_uint(0x18, 6)
    .store_slice(jetton_master_address)
    .store_coins(0)
    .store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .store_ref(msg_body);

  send_raw_message(msg.end_cell(), 64);

  save_data(balance, owner_address, jetton_master_address, jetton_wallet_code);
}
```


Basically, `burn_tokens` is for reducing. We read the data, we get the `query_id`, the amount of coins to burn, whom to send the response, and then we reduce the balance. So then we check if the sender is the owner, if the balance is still more than 0. We make sure that the amount of money that comes with it is enough, the contract itself will survive after we send this transaction and be able to pay for the rent. Then we compose the burn notification, just like we reviewed it.

So here we have `burn_notification.` We are sending `burn_notification` as a message body to the master contract. We put the amount, the owner, the response. Do you remember that as a response, we also implemented whom to send response, right? We actually composed the message cell. Here we put the message body and we send the raw message with this information and message body. And then we just send `save_data` because the balance has changed.


### On_bounce

The last thing here that we are going to review in this lesson is `on_bounce`:


```
() on_bounce (slice in_msg_body) impure {
  in_msg_body~skip_bits(32); ;; 0xFFFFFFFF
  (int balance, slice owner_address, slice jetton_master_address, cell jetton_wallet_code) = load_data();
  int op = in_msg_body~load_uint(32);
  throw_unless(709, (op == op::internal_transfer()) | (op == op::burn_notification()));
  int query_id = in_msg_body~load_uint(64);
  int jetton_amount = in_msg_body~load_coins();
  balance += jetton_amount;
  save_data(balance, owner_address, jetton_master_address, jetton_wallet_code);
}
```


What do we do when the message is actually bounced? It will bounce with a certain error, so we will read our balance and check that the operation code is either `internal_transfer` or `burn_notification` bounce messages. If it's one of those, we will have to put the amount of Jettons that were not accepted by another contract, back into our contract.


### Conclusion

So this is the core logic of Jetton wallet contract. I hope it doesn't confuse you a lot. This is a great example for you to actually read the code and see how complex it can be. It is going to help you a lot if you spend some time reading those contracts. If you don't understand certain things like lines or bits in cells, you can easily text in groups or read the documentation. The purpose of this lesson was to give you the higher overview of how it works with Jettons, so you can understand this logic with different contracts interacting with each other along with a standard.

In the next lesson, we are going to have a closer look into a different type of token, which is called non-fungible token (NFT). This will be interesting for you because we have collection contracts, item contracts there. You already passed this very hard part and now you understand there can be multiple contracts. Jetton is not a single contract, it's a system of contracts. And with NFTs, it will also be very interesting for you to see how they work under the hood according to the standard. So see you very soon!

[Proceed to Lesson 4 >](https://github.com/markokhman/func-course/blob/main/Chapter%207/Lesson%204.md)

[< Go to previous lesson ](https://github.com/markokhman/func-course/blob/main/Chapter%207/Lesson%202.md)