# Chapter 7. Lesson 5. NFT item / SBT item smart contract

###### tags: `Chapter 7`

In this lesson, we are going to review the code of **NFT** item, a classic one. And also, we are going to learn about the SoulBound (**SBT**) item.

### NFT item contract
Let’s open and review the [contract](https://github.com/ton-blockchain/token-contract/blob/main/nft/nft-item.fc).


```
int min_tons_for_storage() asm "50000000 PUSHINT"; ;; 0.05 TON
```


Just like in the previous lessons, we see that there are certain constants that are showing what is the minimum of coins we should actually store on the contract to be able to pay the rent.


```
(int, int, slice, slice, cell) load_data() {
    slice ds = get_data().begin_parse();
    var (index, collection_address) = (ds~load_uint(64), ds~load_msg_addr());
    if (ds.slice_bits() > 0) {
      return (-1, index, collection_address, ds~load_msg_addr(), ds~load_ref());
    } else {  
      return (0, index, collection_address, null(), null()); ;; nft not initialized yet
    }
}
```


Then we see `load_data`. And this load_data is a little different from what we used to see. Usually we were just reading data from the local storage, but here we have some more complex logic. So we open the cell of the local storage for reading and then we read out the `index` and the `collection_address`. We know what index and collecton address are.

Then we try to read out metadata of the NFT item. First, we check if it's present there. If yes, we return `–1`, along with the collection address, the address of the owner and the metadata. Or we just return `0`, index and collection address, when there is no owner and no metadata. It means NFT is not initialized yet. Based on this, we’ll make some decisions later in the code.


```
() store_data(int index, slice collection_address, slice owner_address, cell content) impure {
    set_data(
        begin_cell()
            .store_uint(index, 64)
            .store_slice(collection_address)
            .store_slice(owner_address)
            .store_ref(content)
            .end_cell()
    );
}
```


We also see the `store_data` is just regular, like it always was in other contracts that we look into.


```
() send_msg(slice to_address, int amount, int op, int query_id, builder payload, int send_mode) impure inline {
  var msg = begin_cell()
    .store_uint(0x10, 6) ;; nobounce - int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 010000
    .store_slice(to_address)
    .store_coins(amount)
    .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
    .store_uint(op, 32)
    .store_uint(query_id, 64);

  if (~ builder_null?(payload)) {
    msg = msg.store_builder(payload);
  }
```


And here we encapsulate the logic of sending message, so we can use this function in the rest of the logic of our contract. So whenever we want to send a message, we can actually just use this function. It accepts destination address, the amount that we should attach, the operation code, the query ID, the possible payload (in the builder format this time), and the send mode of the message. This is how we form the logic of sending messages.

Another function we have is `transfer_ownership`. Let's look into it later once we get to it in the `recv_internal` logic, because it is the most important function here.


```
() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore empty messages
        return ();
    }

    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }
    slice sender_address = cs~load_msg_addr();

    cs~load_msg_addr(); ;; skip dst
    cs~load_coins(); ;; skip value
    cs~skip_bits(1); ;; skip extracurrency collection
    cs~load_coins(); ;; skip ihr_fee
    int fwd_fee = muldiv(cs~load_coins(), 3, 2); ;; we use message fwd_fee for estimation of forward_payload costs

    (int init?, int index, slice collection_address, slice owner_address, cell content) = load_data();
    if (~ init?) {
      throw_unless(405, equal_slices(collection_address, sender_address));
      store_data(index, collection_address, in_msg_body~load_msg_addr(), in_msg_body~load_ref());
      return ();
    }
```


Let's see what it does. Again, we ignore the empty `in_msg_body` messages, read the flags, ignore the bounced messages, and we check who's the sender. Now we skip some things like the destination address of the message and stuff, because you already know what the contents of this cell are. Next we have `fwd_fee` again to estimate how much it will cost to send the next transaction based on the previous transaction.

Then we load data. As you remember, we actually load the data the same way as implemented here. So we check that it's not initialized, then we expect to actually get this data, expect the `in_msg_body` to have the address of the owner and the cell with metadata. But this can be done only by `collection_address`, so we check that it is equal to sender address. Then we can initialize the NFT item.


```
int op = in_msg_body~load_uint(32);
  int query_id = in_msg_body~load_uint(64);

  if (op == op::transfer()) {
    transfer_ownership(my_balance, index, collection_address, owner_address, content, sender_address, query_id, in_msg_body, fwd_fee);
    return ();
  }
  if (op == op::get_static_data()) {
    send_msg(sender_address, 0, op::report_static_data(), query_id, begin_cell().store_uint(index, 256).store_slice(collection_address), 64);  ;; carry all the remaining value of the inbound message
    return ();
  }
```


Next we have the operation code that can be `op::transfer` or `op::get_static_data`. These are the two possible values that we are going to handle. When we say `get_static_data`, we immediately send a message to the one who sent a request for the static data. We just report the data, so we send him the `index` and the `collection_address`. This is the `get_static_data` functionality. And as you see, this is our first use case of `send_ msg`.


```
() transfer_ownership(int my_balance, int index, slice collection_address, slice owner_address, cell content, slice sender_address, int query_id, slice in_msg_body, int fwd_fees) impure inline {
    throw_unless(401, equal_slices(sender_address, owner_address));

    slice new_owner_address = in_msg_body~load_msg_addr();
    force_chain(new_owner_address);
    slice response_destination = in_msg_body~load_msg_addr();
    in_msg_body~load_int(1); ;; this nft don't use custom_payload
    int forward_amount = in_msg_body~load_coins();
    throw_unless(708, slice_bits(in_msg_body) >= 1);

    int rest_amount = my_balance - min_tons_for_storage();
    if (forward_amount) {
      rest_amount -= (forward_amount + fwd_fees);
    }
    int need_response = response_destination.preload_uint(2) != 0; ;; if NOT addr_none: 00
    if (need_response) {
      rest_amount -= fwd_fees;
    }

    throw_unless(402, rest_amount >= 0); ;; base nft spends fixed amount of gas, will not check for response

    if (forward_amount) {
      send_msg(new_owner_address, forward_amount, op::ownership_assigned(), query_id, begin_cell().store_slice(owner_address).store_slice(in_msg_body), 1);  ;; paying fees, revert on errors
    }
    if (need_response) {
      force_chain(response_destination);
      send_msg(response_destination, rest_amount, op::excesses(), query_id, null(), 1); ;; paying fees, revert on errors
    }

    store_data(index, collection_address, new_owner_address, content);
}
```


For transferring ownership we have a function here. First, we check that the owner address of the item is currently the one’s who is sending the message, because only he can transfer ownership. Then we read out the address of the new owner. We `force_chain` to make sure that he is on the same chain. Then we check if there is a `response_destination`, so if we should tell someone to send a message to some other one once we transfer this ownership. We check what the `forward_amount` is, if it's requested. Then we figure out that we’ll have enough money after we send this `forward_amount` to the destination. Then we figure out if the response is even needed, if the `response_destination` is present. If it's present, the rest amount must be bigger than 0.

I'll show you briefly how it works. If the `forward_amount` is present there, we are going to send the message with a notification that ownership is assigned with this `forward_amount`. If we need the response, we `force_chain` again to make sure that the response destination is on the same chain. And we send the response, which usually includes excesses and all the extra funds; they're just forwarded to the `response_destination`. Then we just save data with the `new_owner_address`. Basically, this is the only thing that is going to change, `transfer_ownership` is one of the core functions here. You have an item, and sometimes you transfer the ownership to other people or sell it, depending on the type of this deal.

### SBT item contract
NFTs can operate at another level. Check out the [Getgems](https://getgems.io/) marketplace. They have their own contracts that make it possible to sell this kind of assets. We won't research these marketplace contracts now. Still, I'll show you a place where you can learn a lot about NFTs — [GetGems github](https://github.com/getgems-io) — there we'll find one more type of NFTs that I want to overview for you.

[GetGems repository](https://github.com/getgems-io/nft-contracts) is hosting a number of different contracts of NFTs, collections, sale and marketplace and some auctions. This is a good place to learn more about the contracts that are related to NFT collections, items and whatever is surrounding them. But I wanted to show you one exact contract that we can dig into a little.

The [sbt-item](https://github.com/getgems-io/nft-contracts/blob/main/packages/contracts/sources/sbt-item.fc) is a SoulBound token (SBT). Soulbound is very similar to NFT, but the difference is that soulbound tokens cannot be transferred to other people without the authority. Let’s have a closer look at its code.


```
global int storage::index;
global int init?;
global slice storage::collection_address;
global slice storage::owner_address;
global slice storage::authority_address;
global cell storage::content;
global int storage::revoked_at;
() load_data() impure {
    slice ds = get_data().begin_parse();

    storage::index              = ds~load_uint(64);
    storage::collection_address = ds~load_msg_addr();
    init?                       = false;

    if (ds.slice_bits() > 0) {
        init?                      = true;
        storage::owner_address     = ds~load_msg_addr();
        storage::content           = ds~load_ref();
        storage::authority_address = ds~load_msg_addr();
        storage::revoked_at        = ds~load_uint(64);
    }
}
```

We have some variables here: `storage::index`, `storage::collection_address`. We define other global variables as well, and this is just an example of implementation by GetGems. You don't have to do it the same way. I wanted to focus on some other parts here:


```
if (op == op::request_owner()) {
    slice dest = in_msg_body~load_msg_addr();
    cell body = in_msg_body~load_ref();
    int with_content = in_msg_body~load_uint(1);

    var msg = begin_cell()
            .store_uint(storage::index, 256)
            .store_slice(sender_address)
            .store_slice(storage::owner_address)
            .store_ref(body)
            .store_uint(storage::revoked_at, 64)
            .store_uint(with_content, 1);

    if (with_content != 0) {
        msg = msg.store_ref(storage::content);
    }

    ;; mode 64 = carry all the remaining value of the inbound message
    send_msg(flag::regular() | flag::bounce(), dest, 0, op::owner_info(), query_id, msg, 64);
    return ();
}
if (op == op::prove_ownership()) {
    throw_unless(401, equal_slices(storage::owner_address, sender_address));

    slice dest = in_msg_body~load_msg_addr();
    cell body = in_msg_body~load_ref();
    int with_content = in_msg_body~load_uint(1);

    var msg = begin_cell()
            .store_uint(storage::index, 256)
            .store_slice(storage::owner_address)
            .store_ref(body)
            .store_uint(storage::revoked_at, 64)
            .store_uint(with_content, 1);

    if (with_content != 0) {
        msg = msg.store_ref(storage::content);
    }

    ;; mode 64 = carry all the remaining value of the inbound message
    send_msg(flag::regular() | flag::bounce(), dest, 0, op::ownership_proof(), query_id, msg, 64);
    return ();
}
if (op == op::get_static_data()) {
    var msg = begin_cell().store_uint(storage::index, 256).store_slice(storage::collection_address);

    ;; mode 64 = carry all the remaining value of the inbound message
    send_msg(flag::regular(), sender_address, 0, op::report_static_data(), query_id, msg, 64);
    return ();
}
if (op == op::destroy()) {
    throw_unless(401, equal_slices(storage::owner_address, sender_address));

    send_msg(flag::regular(), sender_address, 0, op::excesses(), query_id, null(), 128);

    storage::owner_address = null_addr();
    storage::authority_address = null_addr();
    store_data();
    return ();
}
if (op == op::revoke()) {
    throw_unless(401, equal_slices(storage::authority_address, sender_address));
    throw_unless(403, storage::revoked_at == 0);

    storage::revoked_at = now();
    store_data();
    return ();
}
if (op == op::take_excess()) {
    throw_unless(401, equal_slices(storage::owner_address, sender_address));

    ;; reserve amount for storage
    raw_reserve(min_tons_for_storage(), 0);

    send_msg(flag::regular(), sender_address, 0, op::excesses(), query_id, null(), 128);
    return ();
}
if (op == op::transfer()) {
    throw(413);
}
```


As you can see, here we have a number of comments, but we're not going to dig into each line. You see that this token is going to be used in so many different ways, but not like normal NFTs. Here is the contract, and you can request an owner of this contract. Some operation code can request proof of ownership. You can get static data. You can also destroy or revoke this token. You can take the excess, like, some money that was stored there. And you can request it if you are the owner of this contract. But you can not transfer it obviously, just like you see it here. This is a whole different type of tokens and you should spend some more time in this GetGems repository if you're into NFTs.

I want to show you that the NFT collection can stay the same with all its code, but the items that the collection contract is deploying could be different. For example, you can replace the NFT item with the SBT item. So you will have a collection of SBT teams, and then you would just change the [code](https://github.com/ton-blockchain/token-contract/blob/main/nft/nft-collection.fc) that initializes your collection:


```
(slice, int, cell, cell, cell) load_data() inline {
  var ds = get_data().begin_parse();
  return 
    (ds~load_msg_addr(), ;; owner_address
     ds~load_uint(64), ;; next_item_index
     ds~load_ref(), ;; content
     ds~load_ref(), ;; nft_item_code
     ds~load_ref()  ;; royalty_params
     );
}
```


This way you would actually deploy the collection of SBT items. Also you can create a single, independent [SBT item](https://github.com/getgems-io/nft-contracts/blob/main/packages/contracts/sources/sbt-single.fc) without a collection. We won't master the programming of these items; I just wanted to show you how it works on the bigger scale.

### Conclusion
From the very simple logic of [chapter 3](https://github.com/markokhman/func-course/tree/main/Chapter%203) we got all the way to understanding so many things in the complex contracts like NFTs or Jettons. This is a good example that step by step you can learn certain syntax and concepts, and then  more complex logic is open for you. Hopefully these few lessons helped you to understand what Jettons and NFTs really are and how to approach their codebases — to figure out how they work and learn more in terms of syntax and architecture available for you when you're building in TON.

I really appreciate your attention. This might be the hardest chapter for you, though we didn't code anything; we were just reading some stuff that you have never seen before and it's very complex. So I'm really proud that you made it all the way to this last lesson and I'm looking forward to seeing you in the next chapters. Hope you enjoy the course so far.

[< Go to previous lesson ](https://github.com/markokhman/func-course/blob/main/Chapter%207/Lesson%204.md)